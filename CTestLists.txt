#
#   Unique CTest script
#
#   Copyright Olivier Le Doeuff 2019
#

# DEPENDENCIES

set(GTEST_REPOSITORY "https://github.com/google/googletest" CACHE STRING "Repository of googletest")
set(GTEST_TAG "master" CACHE STRING "Git tag/branch of googletest")

include(FetchContent)

# googletest
FetchContent_Declare(
    googletest
    GIT_REPOSITORY ${GTEST_REPOSITORY}
    GIT_TAG        ${GTEST_TAG}
)

enable_testing()

set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
FetchContent_MakeAvailable(googletest)

set_target_properties(gtest gtest_main gmock gmock_main
    PROPERTIES FOLDER Dependencies/gtest)

set(UNIQUE_TEST_TARGET ${UNIQUE_TESTS_PREFIX}_Tests)

set(UNIQUE_TEST_TARGET_DIR ${CMAKE_CURRENT_SOURCE_DIR}/tests)

set(UNIQUE_TEST_SRCS  ${UNIQUE_TEST_TARGET_DIR}/Tests.cpp
        ${UNIQUE_TEST_TARGET_DIR}/IdProviderTests.cpp
        ${UNIQUE_TEST_TARGET_DIR}/MapTests.cpp
    )

message(STATUS "Add Test: ${UNIQUE_TEST_TARGET}")

# Create the executable
add_executable(${UNIQUE_TEST_TARGET} ${UNIQUE_TEST_SRCS})
target_link_libraries(${UNIQUE_TEST_TARGET} ${UNIQUE_TARGET})
target_link_libraries(${UNIQUE_TEST_TARGET} gtest)
set_target_properties(${UNIQUE_TEST_TARGET} PROPERTIES FOLDER ${UNIQUE_FOLDER_PREFIX}/Tests)

if(MSVC)
    target_compile_definitions(${UNIQUE_TEST_TARGET} PRIVATE "-D_CRT_SECURE_NO_WARNINGS")
endif()

# Register the test
add_test(NAME ${UNIQUE_TEST_TARGET} COMMAND ${UNIQUE_TEST_TARGET})
